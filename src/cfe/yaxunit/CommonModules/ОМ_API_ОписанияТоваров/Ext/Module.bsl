#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
    
   ЮТТесты.ДобавитьТестовыйНабор("API ОписанияТоваров: Получить товар по артикулу", "TASK-1, Каратаев Олег").ВТранзакции()
   		.ДобавитьТест("ПолучитьОписаниеТоваровПоАртикулуПозитив", "Описание товаров по артикулу", "Позитив")
   		.ДобавитьТест("ПолучитьОписаниеТоваровПоАртикулуНегатив", "Описание товаров по несуществующему артикулу", "Негатив")
   		.ДобавитьТест("ПолучитьОписаниеТоваровПоАртикулуПозитивСоздание", "Описание товаров по артикулу с созданием товара", "Позитив")
	;

КонецПроцедуры

Процедура ПолучитьОписаниеТоваровПоАртикулуПозитив() Экспорт
	
	// 1. Подготовка параметров и генерация данных для выполнения функции.
	Артикул = "Bos0009";	
	
	// 2. Выполнение функции.
	РезультатФункции = API_ОписанияТоваров.ПолучитьТоварПоАртикулу(Артикул);
	
	// 3. Проверка результата.
	ЮТест.ОжидаетЧто(РезультатФункции)
		.ИмеетТип("СправочникОбъект.Товары")
		.Свойство("Артикул").Равно(Артикул)
		.Свойство("Наименование").Равно("Босоножки")
		;
		
КонецПроцедуры 

Процедура ПолучитьОписаниеТоваровПоАртикулуНегатив() Экспорт
	
	Артикул = "1111111122222222222244444";
	
    Товар = API_ОписанияТоваров.ПолучитьТоварПоАртикулу(Артикул);
	
	ЮТест.ОжидаетЧто(Товар)
		.ИмеетТип("Неопределено")
		;

КонецПроцедуры

Процедура ПолучитьОписаниеТоваровПоАртикулуПозитивСоздание() Экспорт
	
	// Описание программного создания: https://bia-technologies.github.io/yaxunit/docs/features/test-data/data-generation/
	
	// 1. Подготовка параметров и генерация данных для выполнения функции.
	
	Артикул = "Test1234";
	Наименование = "Test_Хлеб";
	
	// Создание товара
	НовыйТовар = ЮТест.Данные().КонструкторОбъекта("Справочники.Товары")
		.ФикцияОбязательныхПолей()
		.Установить("Артикул", Артикул) // проверочный артикул
		.Установить("Наименование", Наименование) // проверочное наименование
		.ФикцияРеквизитов("Поставщик, Вид, Штрихкод, Описание")
		.Записать()
		;
			
	// 2. Выполнение функции.
    Товар = API_ОписанияТоваров.ПолучитьТоварПоАртикулу(Артикул);

	// 3. Проверка результата.
	ЮТест.ОжидаетЧто(Товар)
		.ИмеетТип("СправочникОбъект.Товары")
		.Свойство("Артикул").Равно(Артикул)
		.Свойство("Наименование").Равно(Наименование)
		;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#КонецОбласти
